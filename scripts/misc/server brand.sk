# Server brand changer
#
# This script can be used to change the sent server brand for specific clients.
# The server brand is displayed in a few locations in the client, the most visible
# one being the debug (F3) menu.
# This facilitates further customisation of servers :)

# Authored by Mr. Darth.
# Tested using Skript 2.6.3 and skript-reflect 2.3 on Paper 1.18.2.



import:
	org.bukkit.Bukkit
	ch.njol.skript.Skript
	java.nio.charset.StandardCharsets

# Sends a new server brand to the specified players.
effect (send|show) [server] brand %string% (to|for) %players%:
	parse:
		Bukkit.getMessenger().registerOutgoingPluginChannel(Skript.getInstance(), "minecraft:brand")
		continue
	trigger:
		set {_string::*} to ...expr-1.getBytes(StandardCharsets.UTF_8)
		loop 255 times:
			set {_bytes::%loop-value%} to {_string::%loop-value%} ? 0
		loop exprs-2:
			loop-value.addChannel("minecraft:brand")
			loop-value.sendPluginMessage(Skript.getInstance(), "minecraft:brand", [(size of {_string::*}), {_bytes::*}])